buildscript {
  repositories {
    mavenCentral()
    google()
  }
}

apply plugin: 'com.android.application'

android {
  namespace "io.github.fortheoil"
  compileSdk 35

  sourceSets {
    main {
      manifest.srcFile 'AndroidManifest.xml'
      java.setSrcDirs(['src/main/java'])
      aidl.setSrcDirs(['src/main/java'])
      renderscript.setSrcDirs(['src/main/java'])
      res.setSrcDirs(['res'])
      assets.setSrcDirs(['../assets'])
      jniLibs.setSrcDirs(['libs'])
    }
  }

  packagingOptions {
    resources {
      excludes += [
        'META-INF/robovm/ios/robovm.xml',
        'META-INF/DEPENDENCIES.txt',
        'META-INF/DEPENDENCIES',
        'META-INF/dependencies.txt',
        '**/*.gwt.xml'
      ]
      pickFirsts += [
        'META-INF/LICENSE.txt',
        'META-INF/LICENSE',
        'META-INF/license.txt',
        'META-INF/LGPL2.1',
        'META-INF/NOTICE.txt',
        'META-INF/NOTICE',
        'META-INF/notice.txt'
      ]
    }
  }

  defaultConfig {
    applicationId 'io.github.foirtheoil'
    minSdkVersion 21
    targetSdkVersion 35
    versionCode 1
    versionName "1.0"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
    coreLibraryDesugaringEnabled true
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

repositories {
  google()
  mavenCentral()
}

configurations { natives }

dependencies {
  // UI Android standard
  implementation 'androidx.appcompat:appcompat:1.7.1'
  coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

  // LibGDX backend et cÅ“ur du projet
  implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
  implementation project(':core')

  implementation 'androidx.viewpager2:viewpager2:1.1.0'
  implementation 'androidx.core:core:1.13.1'
  implementation 'androidx.constraintlayout:constraintlayout:2.1.4'


  // Librairies natives LibGDX
  natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
  natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
  natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
  natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"

  natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
  natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
  natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
  natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
  natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
}

// Copie les .so LibGDX dans les bons dossiers libs/
tasks.register('copyAndroidNatives') {
  doFirst {
    file("libs/armeabi-v7a/").mkdirs()
    file("libs/arm64-v8a/").mkdirs()
    file("libs/x86_64/").mkdirs()
    file("libs/x86/").mkdirs()

    configurations.natives.copy().files.each { jar ->
      def outputDir = null
      if (jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
      if (jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
      if (jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
      if (jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")
      if (outputDir != null) {
        copy {
          from zipTree(jar)
          into outputDir
          include "*.so"
        }
      }
    }
  }
}

tasks.matching { it.name.contains("merge") && it.name.contains("JniLibFolders") }.configureEach { packageTask ->
  packageTask.dependsOn 'copyAndroidNatives'
}

// Commande pour lancer l'appli via Gradle
tasks.register('run', Exec) {
  def path
  def localProperties = project.file("../local.properties")
  if (localProperties.exists()) {
    Properties properties = new Properties()
    localProperties.withInputStream { instr ->
      properties.load(instr)
    }
    def sdkDir = properties.getProperty('sdk.dir')
    if (sdkDir) {
      path = sdkDir
    } else {
      path = "$System.env.ANDROID_SDK_ROOT"
    }
  } else {
    path = "$System.env.ANDROID_SDK_ROOT"
  }

  def adb = path + "/platform-tools/adb"
  commandLine "$adb", 'shell', 'am', 'start', '-n', 'io.github.some_example_name/io.github.some_example_name.android.AndroidLauncher'
}

// Fix des conflits Kotlin transitoires (sans activer Kotlin)
configurations.all {
  resolutionStrategy.eachDependency { details ->
    if (details.requested.group == "org.jetbrains.kotlin") {
      details.useVersion "1.8.22"
      details.because "Force unique Kotlin version for transitive dependencies"
    }
  }
}

eclipse.project.name = appName + "-android"
